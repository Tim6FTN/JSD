import wash_internal

WashScript:
    (
        import_statements*=ImportStatement
        ('use configuration' configuration=[Configuration])?
        open_statement=OpenStatement
        expressions*=Expression
    )
      |
    (
        configuration_definitions*=Configuration
    )
;

ImportStatement:
    'import' importURI=STRING
;

OpenStatement:
    OpenURLStatement | OpenFileStatement | OpenStringStatement
;

OpenURLStatement:
    'open' url=STRING
;

OpenFileStatement:
    'file' file_path=STRING
;

OpenStringStatement:
    'html' html=STRING
;

Configuration:
    'define configuration' name=ID '{'
        ('description' ':' description=STRING)?
        configuration_entries+=ConfigurationEntry
    '}'
;

ConfigurationEntry:
    'option' type=[ConfigurationOption] '{'
        parameters*=ConfigurationParameterValue
    '}'
;

ConfigurationParameterValue:
    parameter=[ConfigurationOptionParameter] ':' value=Value
;

// TODO: Re-check if this value should be moved to internal language definition.
Value:
    PrimitiveValue | ArrayValue
;

PrimitiveValue:
    value=STRING | value=STRICTFLOAT | value=INT | value=BOOL
;

ArrayValue:
    '[' value+=STRING[','] ']' | '[' value+=STRICTFLOAT[','] ']' | '[' value+=INT[','] ']' | '[' value+=BOOL[','] ']'
;

Expression:
    StaticExpression | DynamicExpression
;

StaticExpression:
    queries+=Query (context_expression=ContextExpression)? '->' result_key=ID            // TODO: Result key optional
;

Query:
    SelectorQuery | DataQuery
;

SelectorQuery:
    IndexSelectorQuery |
    IDSelectorQuery |
    NameSelectorQuery |
    TagSelectorQuery |
    ClassSelectorQuery |
    CSSSelectorQuery | 
    XPathSelectorQuery
;

IndexSelectorQuery: '?i ' query_value=QueryValue;
IDSelectorQuery: '?id' query_value=QueryValue;
NameSelectorQuery: '?n' query_value=QueryValue;
TagSelectorQuery: '?t' query_value=QueryValue;
ClassSelectorQuery: '?cl' query_value=QueryValue;
CSSSelectorQuery: '?c ' query_value=QueryValue;
XPathSelectorQuery: '?x' query_value=QueryValue;

DataQuery: ':' query_value=QueryValue;

QueryValue:
/*
    This regex will capture all characters until one of the following is met:
    - '->'      (result key)
    - '?'       (start of a selector query)
    - ':'       (start of a data query)
    - '{'       (start of a context expression)
    - '\n'      (new line)
    This enables the user to write queries without quotes.
*/
    value=/(.|\n)+?(?=(->|\?|:|{|\n))|[^?:{]*/
;

ContextExpression:
    '{' expressions+=StaticExpression '}'
;

DynamicExpression:
        MouseEventCommand | 
        ScriptExecutionCommand | 
        KeyboardEventCommand | 
        WaitCommand | 
        NavigationCommand
;

MouseEventCommand:
    'click on' element_selector_queries+=SelectorQuery
;

ScriptExecutionCommand:
    'execute script' script=STRING
;

KeyboardEventCommand:
    'type' value=STRING ('into' element_selector_queries+=SelectorQuery)?
;

WaitCommand:
    SleepCommand | ExplicitWaitCommand
;

SleepCommand:
    'wait for' value=SleepCommandValue
;

SleepCommandValue: STRICTFLOAT | INT;

ExplicitWaitCommand:
    'wait until' selector_query=SelectorQuery ':' rule=ExplicitWaitCommandRule (',' 'timeout after' timeout_value=TimeoutValue)?
;

ExplicitWaitCommandRule:
    'present' | 'visible' | 'clickable'
;

TimeoutValue: STRICTFLOAT | INT;

NavigationCommand:
    'go to' url=STRING
;

Comment:
  /\*\*.*$/
;